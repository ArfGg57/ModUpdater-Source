apply plugin: 'java'

group = 'com.ArfGg57.modupdater'
version = '2.47'
archivesBaseName = '!!!!!modupdater-mod'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net/' }
    maven { url = 'https://libraries.minecraft.net' }
}

dependencies {
    implementation project(':modupdater-core')
    // Use the forgeSrc jar from core libs for compile-time
    compileOnly files('../modupdater-core/libs/forgeSrc-1.7.10-10.13.4.1614-1.7.10.jar')
}

// Fat jar that includes core classes and dependencies
jar {
    manifest {
        attributes(
            'Implementation-Title': archivesBaseName,
            'Implementation-Version': version
        )
    }
    // Embed all required classes directly (fat jar)
    from { sourceSets.main.output }
    from { project(':modupdater-core').sourceSets.main.output }
    // Unpack third-party dependency jars (e.g., json) but skip ForgeSrc (already provided by environment)
    from {
        configurations.runtimeClasspath
            .filter { !(it.name =~ /forgeSrc-.*\.jar/) }
            .collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude 'META-INF/*.SF','META-INF/*.DSA','META-INF/*.RSA'
}

// Process resources (mcmod.info)
processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
